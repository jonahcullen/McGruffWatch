
from pathlib import Path
import pandas as pd

workdir: config['workdir']

regions_path = Path(workflow.basedir).parent / "config" / "dog.regions.txt"
regions = list(pd.read_table(regions_path, header=None)[0])

#regions = "chr1.0-41185489"

rule all:
    input:
       #expand("output/split/{region}.csv", region=regions)
        "output/final/af_analysis.no_intergenic.csv" 
       #"output/final/af_analysis.no_intergenic.csv"
 
rule process_variants:
    input:
        list_dir = config['list_dir'],
        pop_vcf = config['pop_vcf']
    output:
        table = "output/split/{region}.csv"
    params:
        calc_af = Path(workflow.basedir) / "scripts" / "af_analysis_cyvcf.py",
    singularity: config['arf_image']
    threads: 1
    resources:
        time = 720,
        mem_mb = 20000
    shell:
        '''
            source activate cyvcf2_env

            python {params.calc_af} \
                -v {input.pop_vcf} \
                -d {input.list_dir} \
                -r {wildcards.region} \
                > {output.table}
        '''

rule concat_files:
    input:
        expand("output/split/{region}.csv", region=regions)
    output:
        "output/final/af_analysis.csv"  
    threads: 1
    resources:
        time = 60,
        mem_mb = 20000
    shell:
        '''
            cat {input} |awk 'NR==1 || !/chrm/' > {output} 
        '''

localrules: remove_intergenic
rule remove_intergenic:
    input:
        "output/final/af_analysis.csv" 
    output:
        "output/final/af_analysis.no_intergenic.csv" 
    shell:
        '''
            awk 'NR == 1 || !/intergenic/' {input} > {output}
        '''

